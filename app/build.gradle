plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 31
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "sergio.sastre.multiplying.quality.of.unittests"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        freeCompilerArgs = [
                "-Xjsr305=strict", // Strict interpretation of nullability annotations in jqwik API
                "-Xemit-jvm-type-annotations" // Enable annotations on type variables
        ]
        jvmTarget = '1.8'
        javaParameters = true // Get correct parameter names in jqwik reporting
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform {
                includeEngines 'jqwik', 'junit-jupiter', 'junit-vintage'
            }
            include '**/*Properties.class'
            include '**/*Test.class'
            include '**/*Tests.class'
            include '**/*Examples.class'
        }
    }
    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        exclude "META-INF/*"
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.1"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.8.1"
    testImplementation "com.google.truth:truth:1.1.2"

    testImplementation("net.jqwik:jqwik:1.6.0")
    testImplementation("net.jqwik:jqwik-kotlin:1.6.0")

    // Core library
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Jqwik for instrumented tests
    androidTestImplementation("net.jqwik:jqwik:1.6.0")
    androidTestImplementation("net.jqwik:jqwik-kotlin:1.6.0")

    // Jqwik requires Junit5
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation("org.junit.platform:junit-platform-runner:1.8.1")

}